plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

repositories {
    maven {
        url = 'https://maven.blamejared.com'
    }
}

dependencies {
    implementation("foundry.veil:veil-neoforge-$minecraft_version:$veil_version") {
        exclude group: "maven.modrinth"
        exclude group: "me.fallenbreath"
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable access transformer and interface injection if the files exist
    def at = project(':common').file('src/main/resources/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    def ii = project(':common').file('src/main/resources/interfaces.json')
    if (ii.exists()) {
        interfaceInjectionData.from(ii.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
//            devLogin = true
        }
        data {
            data()

            // DataGen can be run by - "./gradlew :neoforge:runData" in Terminal.
            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources {
    srcDir project(':common').file('src/generated/resources')
}